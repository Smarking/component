/*copy form genApiBuild.gradle*/

import com.android.build.gradle.internal.dependency.AarTransform
import com.android.build.gradle.internal.dependency.ExtractAarTransform
import com.android.build.gradle.internal.publishing.AndroidArtifacts.ArtifactType

import java.util.regex.Pattern

/**
 * 待评估
 * 1\对依赖方式：工程依赖、AAR依赖的影响
 * 2\对ci的影响
 * 3\对热修复打包的影响
 * 4\对动态化的影响
 * 5\对发版的影响
 */

apply plugin: 'java-library'

def SUPPORT_LIBS_VERSION = '27.0.2'
def targetSdkVersion = 26

sourceSets {
    main {
        java.srcDirs = ['src/main/java/']
    }
}

def artifactType = Attribute.of('artifactType', String)

dependencies {
    project.logger.log(LogLevel.ERROR, "GradleVersion ==> ${project.getGradle().getGradleVersion()}")

    /*
      Android Jar
     */

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def sdkDir = properties.getProperty('sdk.dir')

    if (isGradle3()) {
        compileOnly files("${sdkDir}/platforms/android-${targetSdkVersion}/android.jar")
    } else {
        provided files("${sdkDir}/platforms/android-${targetSdkVersion}/android.jar")
    }

    project.logger.log(LogLevel.ERROR, "Dependencies android ==> ${sdkDir}/platforms/android-${targetSdkVersion}/android.jar")

    /*
      Aars
     */
    registerTransform {
        from.attribute(artifactType, "aar")
        to.attribute(artifactType, "aarExtracted")
        artifactTransform(ExtractAarTransform)
    }
    //DefaultVariantTransformRegistry
    registerTransform {
        from.attribute(artifactType, "aarExtracted")
        to.attribute(artifactType, "jar")
        artifactTransform(AarTransform) { params(ArtifactType.JAR) }
    }

    configurations {
        aar {
            attributes {
                attribute(artifactType, "jar")
            }
        }
    }

    aar "com.android.support:appcompat-v7:$SUPPORT_LIBS_VERSION"
    aar "com.android.support:recyclerview-v7:$SUPPORT_LIBS_VERSION"

    configurations.aar.files.collect {
        /*
          Import the jars extracted from the aar packages
         */
        if (isGradle3()) {
            compileOnly files(it.absolutePath)
        } else {
            provided files(it.absolutePath)
        }

        project.logger.log(LogLevel.ERROR, "Dependencies file ==> ${it.absolutePath}")
    }
}

private boolean isGradle3() {
    Pattern GRADLE_ACCEPTABLE_VERSIONS = Pattern.compile("4\\.\\d*|0\\.\\d*[1-9]\\d*\$")
    if (GRADLE_ACCEPTABLE_VERSIONS.matcher(project.getGradle().getGradleVersion()).matches()) {
        return true
    }
    return false
}